/*
 * Copyright (c) 2017 Villu Ruusmann
 *
 * This file is part of JPMML-SkLearn
 *
 * JPMML-SkLearn is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-SkLearn is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-SkLearn.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.5";
	STATIC = false;
}

PARSER_BEGIN(ExpressionTranslator)
package org.jpmml.sklearn;

import java.util.ArrayList;
import java.util.List;

import numpy.core.UFuncUtil;
import org.dmg.pmml.Constant;
import org.dmg.pmml.DataType;
import org.dmg.pmml.Expression;
import org.dmg.pmml.FieldName;
import org.dmg.pmml.FieldRef;
import org.dmg.pmml.PMMLFunctions;
import org.jpmml.converter.Feature;
import org.jpmml.converter.PMMLUtil;
import org.jpmml.converter.visitors.ExpressionCompactor;
import org.jpmml.model.ValueUtil;

public class ExpressionTranslator extends AbstractTranslator {

	static
	public Expression translate(String string, List<? extends Feature> features){
		return translate(string, features, true);
	}

	static
	public Expression translate(String string, List<? extends Feature> features, boolean compact){
		Expression expression;
		
		try {
			ExpressionTranslator expressionTranslator = new ExpressionTranslator(string);
			expressionTranslator.setFeatures(features);

			expression = expressionTranslator.translateExpressionInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException("Python expression \'" + string + "\' is either invalid or not supported", pe);
		}

		if(compact){
			ExpressionCompactor compactor = new ExpressionCompactor();

			compactor.applyTo(expression);
		}

		return expression;
	}

	static
	private String translateRelationalOperator(Token operator){
		
		switch(operator.kind){
			case EQUAL:
				return PMMLFunctions.EQUAL;
			case NOT_EQUAL:
				return PMMLFunctions.NOTEQUAL;
			case LESS_THAN:
				return PMMLFunctions.LESSTHAN;
			case LESS_OR_EQUAL:
				return PMMLFunctions.LESSOREQUAL;
			case GREATER_THAN:
				return PMMLFunctions.GREATERTHAN;
			case GREATER_OR_EQUAL:
				return PMMLFunctions.GREATEROREQUAL;
			default:
				throw new IllegalArgumentException(operator.image);
		}
	}

	static
	private String translateArithmeticOperator(Token operator){

		switch(operator.kind){
			case MODULO:
				return PMMLFunctions.MODULO;
			default:
				return operator.image;
		}
	}

	static
	private Expression translateFunction(String function, List<Expression> arguments){
		int dot = function.lastIndexOf('.');
		if(dot < 0){
			throw new IllegalArgumentException(function);
		}

		String module = function.substring(0, dot);
		String name = function.substring(dot + 1);

		if(("pandas").equals(module) || ("pd").equals(module)){

			if(arguments.size() != 1){
				throw new IllegalArgumentException();
			}

			switch(name){
				case "isnull":
					return PMMLUtil.createApply(PMMLFunctions.ISMISSING, arguments.get(0));
				case "notnull":
					return PMMLUtil.createApply(PMMLFunctions.ISNOTMISSING, arguments.get(0));
				default:
					break;
			}
		} else

		if(("numpy").equals(module) || ("np").equals(module)){

			if(arguments.size() != 1){
				throw new IllegalArgumentException();
			}

			return UFuncUtil.encodeUFunc("numpy", name, arguments.get(0));
		}

		throw new IllegalArgumentException(function);
	}

	static
	private String translateValue(Token value){
		String image = value.image;

		switch(value.kind){
			case FALSE:
			case TRUE:
				image = image.toLowerCase();
				break;
			case INT:
				if(image.endsWith("l") || image.endsWith("L")){
					image = image.substring(0, image.length() - 1);
				}
				break;
			case STRING:
				image = image.substring(1, image.length() - 1);
				break;
			default:
				break;
		}

		return image;
	}
}
PARSER_END(ExpressionTranslator)

SPECIAL_TOKEN:
{
	" "
	|
	"\t"
}

TOKEN:
{
	< LPAREN: "(" >
	|
	< RPAREN: ")" >
}

TOKEN:
{
	< IF: "if">
	|
	< ELSE: "else" >
	|
	< OR: "or" >
	|
	< AND: "and" >
	|
	< NOT: "not" >
}

TOKEN:
{
	< EQUAL: "==" >
	|
	< NOT_EQUAL: "!=" >
	|
	< LESS_THAN: "<" >
	|
	< LESS_OR_EQUAL: "<=" >
	|
	< GREATER_THAN: ">" >
	|
	< GREATER_OR_EQUAL: ">=" >
}

TOKEN:
{
	< PLUS: "+" >
	|
	< MINUS: "-" >
	|
	< MULTIPLY: "*" >
	|
	< DIVIDE: "/" >
	|
	< MODULO: "%" >
}

TOKEN:
{
	< LBRACKET: "[" >
	|
	< RBRACKET: "]" >
}

TOKEN:
{
	< COMMA: "," >
	|
	< DOT: "." >
}

TOKEN:
{
	< FALSE: "False" >
	|
	< TRUE: "True" >
}

TOKEN:
{
	< INT:
		"0"
		|
		<NONZERO_DIGIT> (<DIGIT>)* ( ["l", "L"] )?
	>
	|
	< FLOAT:
		(<DIGIT>)+ "." (<DIGIT>)* (<EXPONENT>)?
		|
		"." (<DIGIT>)+ (<EXPONENT>)?
		|
		(<DIGIT>)+ <EXPONENT>
	>
	|
	< NAME: <LETTER> (<LETTER> | <DIGIT>)* >
	|
	< STRING:
		"\"" (~["\""])* "\""
		|
		"\'" (~["\'"])* "\'"
	>
	|
	< #DIGIT: ["0" - "9"] >
	|
	< #NONZERO_DIGIT: ["1" - "9"] >
	|
	< #LETTER: ["a" - "z", "A" - "Z", "_"]>
	|
	< #EXPONENT: ["e", "E"] (["+", "-"])? (<DIGIT>)+ >
}

private Expression translateExpressionInternal():
{
	Expression expression;
}
{
	expression = Expression() <EOF>

	{
		return expression;
	}
}

Expression Expression():
{
	Expression expression;
}
{
	expression = IfElseExpression()

	{
		return expression;
	}
}

Expression IfElseExpression():
{
	Expression left;
	Expression center;
	Expression right;
}
{
	left = LogicalOrExpression() ( <IF> center = LogicalOrExpression() <ELSE> right = LogicalOrExpression() { left = PMMLUtil.createApply(PMMLFunctions.IF, center, left, right); } )?

	{
		return left;
	}
}

Expression LogicalOrExpression():
{
	Expression left;
	Expression right;
}
{
	left = LogicalAndExpression() ( <OR> right = LogicalAndExpression() { left = PMMLUtil.createApply(PMMLFunctions.OR, left, right); } )*

	{
		return left;
	}
}

Expression LogicalAndExpression():
{
	Expression left;
	Expression right;
}
{
	left = NegationExpression() ( <AND> right = NegationExpression() { left = PMMLUtil.createApply(PMMLFunctions.AND, left, right); } )*

	{
		return left;
	}
}

Expression NegationExpression():
{
	Token operator = null;
	Expression expression;
}
{
	( operator = <NOT> )? expression = ComparisonExpression()

	{
		if(operator != null){
			return PMMLUtil.createApply(PMMLFunctions.NOT, expression);
		}

		return expression;
	}
}

Expression ComparisonExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = AdditiveExpression() ((operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = AdditiveExpression() { left = PMMLUtil.createApply(translateRelationalOperator(operator), left, right); })?

	{
		return left;
	}
}

Expression AdditiveExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = MultiplicativeExpression() ((operator = <PLUS> | operator = <MINUS>) right = MultiplicativeExpression() { left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right); })*

	{
		return left;
	}
}

Expression MultiplicativeExpression():
{
	Expression left;
	Token operator;
	Expression right;
}
{
	left = UnaryExpression() ((operator = <MULTIPLY> | operator = <DIVIDE> | operator = <MODULO> ) right = UnaryExpression() { left = PMMLUtil.createApply(translateArithmeticOperator(operator), left, right); })*

	{
		return left;
	}
}

Expression UnaryExpression():
{
	Token sign = null;
	Expression expression;
}
{
	(
		sign = <PLUS> expression = UnaryExpression()
		|
		sign = <MINUS> expression = UnaryExpression()
		|
		expression = PrimaryExpression()
	)

	{
		if(sign != null && sign.kind == MINUS){
			expression = PMMLUtil.toNegative(expression);
		}

		return expression;
	}
}

Expression PrimaryExpression():
{
	Expression expression;
}
{
	(
		LOOKAHEAD(<NAME> <LBRACKET>)
		expression = ArrayIndexingExpression()
		|
		expression = FunctionInvocationExpression()
		|
		expression = ParenthesizedExpression()
		|
		expression = LiteralExpression()
	)

	{
		return expression;
	}
}

FieldRef ArrayIndexingExpression():
{
	Token name;
	Token column;
}
{
	(
		LOOKAHEAD(<NAME> <LBRACKET> <INT>)
		name = <NAME> <LBRACKET> column = <INT> <RBRACKET>
		|
		name = <NAME> <LBRACKET> column = <STRING> <RBRACKET>
	)

	{
		if(!("X").equals(name.image)){
			throw new IllegalArgumentException("Name \'" + name.image + "\' is not defined");
		}

		Feature feature;

		switch(column.kind){
			case ExpressionTranslatorConstants.INT:
				{
					int colIndex = Integer.parseInt(column.image);

					feature = getFeature(colIndex);
				}
				break;
			case ExpressionTranslatorConstants.STRING:
				{
					String colName = column.image.substring(1, column.image.length() - 1);

					feature = getFeature(FieldName.create(colName));
				}
				break;
			default:
				throw new IllegalArgumentException();
		}

		return feature.ref();
	}
}

Expression FunctionInvocationExpression():
{
	Token name;
	StringBuilder function = new StringBuilder();
	List<Expression> arguments;
}
{
	name = <NAME> {
		if(("X").equals(name.image)){
			throw new IllegalArgumentException("Name \'X\' refers to a row vector. Use an array indexing expression X[<column index>] or X[<column name>] to refer to a specific row vector element");
		}
		
		function.append(name.image);
	} ( <DOT> name = <NAME> { function.append('.').append(name.image); })* <LPAREN> arguments = ArgumentList() <RPAREN>

	{
		return translateFunction(function.toString(), arguments);
	}
}

List<Expression> ArgumentList():
{
	Expression expression;
	List<Expression> expressions = new ArrayList<Expression>();
}
{
	expression = Expression() { expressions.add(expression); } ( <COMMA> expression = Expression() { expressions.add(expression); })*

	{
		return expressions;
	}
}

Expression ParenthesizedExpression():
{
	Expression expression;
}
{
	<LPAREN> expression = Expression() <RPAREN>

	{
		return expression;
	}
}

Constant LiteralExpression():
{
	Token value;
	DataType dataType;
}
{
	(
		value = <FALSE> { dataType = DataType.BOOLEAN; }
		|
		value = <TRUE> { dataType = DataType.BOOLEAN; }
		|
		value = <INT> { dataType = DataType.INTEGER; }
		|
		value = <FLOAT> { dataType = DataType.DOUBLE; }
		|
		value = <STRING> { dataType = DataType.STRING; }
	)

	{
		Constant constant = new Constant()
			.setValue(translateValue(value))
			.setDataType(dataType);

		return constant;
	}
}
